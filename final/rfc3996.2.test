#
#Check a printer for conformance with rfc3996
#
#
#Usage:
#
#	./ipptool -f FILENAME(file preferentially large, but not too large) printer-uri rfc3996.2.test
#

{
    # get printer attributes and check support for REQUIRED attributes

	NAME "Get printer attributes using Get-Printer-Attributes"
	OPERATION Get-Printer-Attributes

	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR language attributes-natural-language en
	ATTR uri printer-uri $uri

	STATUS successful-ok

	EXPECT charset-configured
	EXPECT charset-supported
	EXPECT compression-supported
	EXPECT document-format-default
	EXPECT document-format-supported
	EXPECT generated-natural-language-supported
	EXPECT ipp-versions-supported
	EXPECT natural-language-configured
	EXPECT pdl-override-supported
	EXPECT printer-is-accepting-jobs
	EXPECT printer-name
	EXPECT printer-state
	EXPECT printer-state-reasons
	EXPECT printer-up-time
	EXPECT printer-uri-supported
	EXPECT queued-job-count
	EXPECT uri-authentication-supported
	EXPECT uri-security-supported
    
	EXPECT operations-supported OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x0016	# Create-Printer-Subscriptions
	EXPECT operations-supported OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x0018	# Get-Subscription-Attributes
	EXPECT operations-supported OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x0019	# Get-Subscriptions
	EXPECT operations-supported OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x001A	# Renew-Subscription
	EXPECT operations-supported OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x001B	# Cancel-Subscription
	EXPECT operations-supported OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 0x001C	# Get-Notifications
	
	EXPECT notify-pull-method-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE "ippget"
	EXPECT notify-events-default OF-TYPE keyword IN-GROUP printer-attributes-tag
	EXPECT notify-max-events-supported OF-TYPE integer IN-GROUP printer-attributes-tag WITH-VALUE >1 COUNT 1
	EXPECT notify-lease-duration-default OF-TYPE integer IN-GROUP printer-attributes-tag WITH-VALUE >0 COUNT 1
	EXPECT notify-lease-duration-supported OF-TYPE integer|rangeOfInteger IN-GROUP printer-attributes-tag WITH-ALL-VALUES >0

    EXPECT notify-events-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE printer-state-changed
    EXPECT notify-events-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE printer-stopped
    EXPECT notify-events-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE job-state-changed
    EXPECT notify-events-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE job-created
    EXPECT notify-events-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE job-completed

	EXPECT ippget-event-life OF-TYPE integer IN-GROUP printer-attributes-tag WITH-VALUE >14	COUNT 1	# checking ippget-event-life
	EXPECT ippget-event-life DEFINE-VALUE eventlife
}

{
    # create printer subscription

	NAME "Create a pull printer subscription"
	OPERATION Create-Printer-Subscription

    GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR language attributes-natural-language en
	ATTR uri printer-uri $uri
    ATTR name requesting-user-name $user

    GROUP subscription-attributes-tag
	ATTR keyword notify-pull-method ippget
	ATTR keyword notify-events printer-config-changed,printer-state-changed
    ATTR octetString notify-user-data ippuser

	STATUS successful-ok

	EXPECT notify-subscription-id OF-TYPE integer IN-GROUP subscription-attributes-tag WITH-VALUE >0 COUNT 1
	DISPLAY notify-subscription-id
}

{
    # disable-printer to create an event

	NAME "Disable-Printer"

	OPERATION Disable-Printer
	VERSION 2.0

	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri

	STATUS successful-ok
}

{
    # enable-printer to create another event and get the printer working again

	NAME "Enable-Printer"

	OPERATION Enable-Printer
	VERSION 2.0

	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri

	STATUS successful-ok
}

{
	NAME "Get-Notifications conformance check (without event-wait mode)"
	OPERATION Get-Notifications

	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR language attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR integer notify-subscription-ids $notify-subscription-id
	ATTR name requesting-user-name $user
	ATTR boolean notify-wait false

	STATUS successful-ok

	EXPECT notify-get-interval OF-TYPE integer IN-GROUP operation-attributes-tag WITH-VALUE <$eventlife,=$eventlife COUNT 1					# rfc3996 requires it to be >= ippget-event-life value,
																																			# but that doesn't seem to be right.
																																			# ippserver also doesn't follow that.
																																			# that is why the condition imposed here is <= ippget-event-life value.
	
	EXPECT notify-subscription-id OF-TYPE integer IN-GROUP event-notification-attributes-tag WITH-VALUE >0 COUNT 1
	EXPECT notify-printer-uri OF-TYPE uri IN-GROUP event-notification-attributes-tag COUNT 1
	EXPECT notify-subscribed-event OF-TYPE keyword IN-GROUP event-notification-attributes-tag WITH-VALUE printer-state-changed COUNT 1
	EXPECT printer-up-time OF-TYPE integer IN-GROUP event-notification-attributes-tag WITH-VALUE >0 COUNT 1									# not working
	EXPECT notify-sequence-number OF-TYPE integer IN-GROUP event-notification-attributes-tag WITH-VALUE >-1 COUNT 1
	EXPECT notify-charset OF-TYPE charset IN-GROUP event-notification-attributes-tag WITH-VALUE "utf-8" COUNT 1
	EXPECT notify-natural-language OF-TYPE naturalLanguage IN-GROUP event-notification-attributes-tag WITH-VALUE "en"	
	EXPECT notify-user-data OF-TYPE octetString IN-GROUP event-notification-attributes-tag COUNT 1   										# not working
	EXPECT notify-text OF-TYPE text IN-GROUP event-notification-attributes-tag
	EXPECT printer-state OF-TYPE enum IN-GROUP event-notification-attributes-tag
	EXPECT printer-state-reasons OF-TYPE keyword IN-GROUP event-notification-attributes-tag
	EXPECT printer-is-accepting-jobs OF-TYPE boolean IN-GROUP event-notification-attributes-tag												# not working
}

{
	NAME "Print file using Print-Job"
	OPERATION Print-Job

	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR language attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR name requesting-user-name $user
	ATTR mimeMediaType document-format $filetype

	GROUP job-attributes-tag
	ATTR integer copies 1

	GROUP subscription-attributes-tag
	ATTR keyword notify-pull-method ippget
	ATTR keyword notify-events job-completed

	FILE $filename

	STATUS successful-ok
	STATUS successful-ok-ignored-or-substituted-attributes

	EXPECT job-id OF-TYPE integer IN-GROUP job-attributes-tag WITH-VALUE >0 COUNT 1
	EXPECT job-uri OF-TYPE uri IN-GROUP job-attributes-tag COUNT 1
	EXPECT notify-subscription-id OF-TYPE integer IN-GROUP subscription-attributes-tag WITH-VALUE >0 COUNT 1
}

{
	NAME "Get-Notifications conformance check (including event wait mode)"
	OPERATION Get-Notifications

	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR language attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR integer notify-subscription-ids $notify-subscription-id
	ATTR name requesting-user-name $user
	ATTR boolean notify-wait true

	STATUS successful-ok
	STATUS successful-ok-events-complete

	EXPECT notify-get-interval OF-TYPE integer IN-GROUP operation-attributes-tag WITH-VALUE <$eventlife,=$eventlife COUNT 1					

	EXPECT notify-subscription-id OF-TYPE integer IN-GROUP event-notification-attributes-tag WITH-VALUE >0 COUNT 1
	EXPECT notify-printer-uri OF-TYPE uri IN-GROUP event-notification-attributes-tag COUNT 1
	EXPECT notify-subscribed-event OF-TYPE keyword IN-GROUP event-notification-attributes-tag WITH-VALUE job-completed COUNT 1
	EXPECT printer-up-time OF-TYPE integer IN-GROUP event-notification-attributes-tag WITH-VALUE >0 COUNT 1									# not working
	EXPECT notify-sequence-number OF-TYPE integer IN-GROUP event-notification-attributes-tag WITH-VALUE >-1 COUNT 1
	EXPECT notify-charset OF-TYPE charset IN-GROUP event-notification-attributes-tag WITH-VALUE "utf-8" COUNT 1
	EXPECT notify-natural-language OF-TYPE naturalLanguage IN-GROUP event-notification-attributes-tag WITH-VALUE "en"	
	EXPECT notify-user-data OF-TYPE octetString IN-GROUP event-notification-attributes-tag COUNT 1   										# not working
	EXPECT notify-text OF-TYPE text IN-GROUP event-notification-attributes-tag
	EXPECT job-id OF-TYPE integer IN-GROUP event-notification-attributes-tag WITH-VALUE $job-id COUNT 1										# job-id doesn't show up but notify-job-id does.
	EXPECT job-state OF-TYPE enum IN-GROUP event-notification-attributes-tag COUNT 1 											
	EXPECT job-state-reasons OF-TYPE keyword IN-GROUP event-Notification-attributes-tag
}